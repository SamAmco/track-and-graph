/*
 *  This file is part of Track & Graph
 *
 *  Track & Graph is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  Track & Graph is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with Track & Graph.  If not, see <https://www.gnu.org/licenses/>.
 */

plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id "androidx.navigation.safeargs.kotlin"
    id "dagger.hilt.android.plugin"
}

android {
    compileSdk 34

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = "17"
        //Using experimental kotlin co-routines api's
        freeCompilerArgs += "-opt-in=kotlin.RequiresOptIn"
    }

    defaultConfig {
        applicationId "com.samco.trackandgraph"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 300802
        versionName "3.8.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix = ".debug"
            resValue "string", "app_name", "Debug Track & Graph"
        }
        debugMinify {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix = ".debug.minify"
            resValue "string", "app_name", "Debug Minify Track & Graph"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            resValue "string", "app_name", "Track & Graph"
            ndk.debugSymbolLevel = "SYMBOL_TABLE"
        }
    }

    buildFeatures {
        dataBinding true
        compose true
    }

    //If you get an error about compose not supporting your kotlin compiler version
    // you might need to look into this:
    //https://developer.android.com/jetpack/androidx/releases/compose-kotlin
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.4"
    }

    namespace 'com.samco.trackandgraph'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(":functions")

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.7.10"
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    //Dependency Injection
    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    //UI
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'

    var composeVersion = '1.5.0-alpha03'

    implementation "androidx.compose.ui:ui:$composeVersion"
    implementation "androidx.compose.ui:ui-tooling:$composeVersion"
    implementation "androidx.compose.foundation:foundation:$composeVersion"
    implementation "androidx.compose.ui:ui-viewbinding:$composeVersion"
    implementation 'androidx.activity:activity-compose:1.7.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-beta01'
    implementation "androidx.compose.runtime:runtime-livedata:$composeVersion"
    // Material design stuff you might need at some point
    implementation "androidx.compose.material:material:$composeVersion"
    implementation "com.google.android.material:compose-theme-adapter:1.2.1"
    implementation 'com.google.accompanist:accompanist-pager:0.28.0'

    // Android KTX
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.6.1"
    implementation 'androidx.fragment:fragment-ktx:1.5.6'

    //Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:2.5.3"
    implementation "androidx.navigation:navigation-ui-ktx:2.5.3"

    //Lifecycle extensions
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.4"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.4"

    //Date/Time
    testImplementation 'org.threeten:threetenbp:1.4.0'

    //Graph drawing
    implementation 'com.androidplot:androidplot-core:1.5.10'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    //Work manager
    api 'androidx.hilt:hilt-work:1.0.0'
    kapt 'androidx.hilt:hilt-compiler:1.0.0'
    implementation "androidx.work:work-runtime-ktx:2.9.0-alpha02"

    testImplementation 'junit:junit:4.13.1'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1'
    testImplementation "androidx.arch.core:core-testing:2.2.0"
}
